
start: (stmt | _TERM)*

?stmt: simple_stmt | compound_stmt
?simple_stmt: (property | variable) _TERM
?compound_stmt: widget | display

widget: "widget" [ NAME ] _args "{" suite "}"
display: "display" [ NAME ] [ _args ] "{" suite "}"

_args: "(" [ NAME ("," NAME)* [ "," ] ] ")"

suite: (simple_stmt)*
property: NAME "=" value_expr
variable: VARNAME "=" value_expr

?value_expr: mul_expr (_add_op mul_expr)*
?mul_expr: value (_mul_op value)*

!_add_op: "+" | "-"
!_mul_op: "*" | "/" | "%"

?value: "[" [ value_expr ("," value_expr)* [ "," ] ] "]" -> array
    |   widget
    |   VARNAME -> var
    |   NAME -> property
    |   number
    |   string
    |   "(" value_expr ")"

number: NUMBER
string: ESCAPED_STRING

VARNAME: /\$[a-zA-Z_]\w*/
NAME: /[a-zA-Z_]\w*/

_TERM: ";"
_NEWLINE: ( /\r?\n[\t ]*/ )+

%ignore CPP_COMMENT
%ignore C_COMMENT
%ignore WS
// %ignore /\\[\t \f]*\r?\n/

%import common (WS, WS_INLINE, NUMBER, ESCAPED_STRING, CPP_COMMENT, C_COMMENT)