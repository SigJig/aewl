
start: (stmt | _TERM)*

?stmt: simple_stmt | compound_stmt | import
?simple_stmt: (property_def | variable_def) _TERM
?compound_stmt: widget_def | display_def | resource_def

import: "import" joined_property

widget_def: "widget" [ NAME ] inherits "{" suite "}"
resource_def: "resource" [ NAME ] inherits_opt "{" suite "}"
display_def: "display" [ NAME ] inherits_opt "{" suite "}"

inherits: "(" NAME ("," NAME)* [ "," ] ")"
?inherits_opt: [ inherits ]

suite: (simple_stmt)*
property_def: NAME "=" value_expr
variable_def: VARNAME "=" value_expr

?value_expr: mul_expr (_add_op mul_expr)*
?mul_expr: union_expr (_mul_op union_expr)*
?union_expr: value ("&" value)*

!_add_op: "+" | "-"
!_mul_op: "*" | "/" | "%"

?value: "[" [ value_expr ("," value_expr)* [ "," ] ] "]" -> array
    |   widget_def
    |   joined_var
    |   VARNAME -> variable
    |   NAME -> property
    |   NUMBER "%" -> percentage
    |   NUMBER -> number
    |   ESCAPED_STRING -> string
    |   "{" suite "}" -> pod
    |   "(" value_expr ")"

?joined_var: VARNAME ("." VARNAME)+
?joined_property: NAME ("." NAME)

VARNAME: /\$[a-zA-Z_]\w*/
NAME: /[a-zA-Z_]\w*/

_TERM: ";"
_NEWLINE: ( /\r?\n[\t ]*/ )+

%ignore CPP_COMMENT
%ignore C_COMMENT
%ignore WS
// %ignore /\\[\t \f]*\r?\n/

%import common (WS, WS_INLINE, NUMBER, ESCAPED_STRING, CPP_COMMENT, C_COMMENT)